---
title: SierpiÅ„ski
author: Ayberk Tosun (j.w.w. MartÃ­n EscardÃ³)
---

<!--
```agda
{-# OPTIONS --cubical --safe #-}

module Sierpinski where

open import Basis hiding (_âˆ¨_)
open import Poset
open import FormalTopology renaming (pos to posâ€²)
open import Cubical.Data.Fin
open import Cubical.Data.Sum
open import Cubical.Data.Empty using () renaming (rec to âŠ¥-rec)
open import Cubical.Data.Nat hiding (Unit)
open import Frame
open import Cofinality
open import CoverFormsNucleus

to-frame : FormalTopology ğ“¤ ğ“¤ â†’ Frame (ğ“¤ âº) ğ“¤ ğ“¤
to-frame = NucleusFrom.L
```
-->

We present a construction of the SierpiÅ„ski frame from a formal topology in
cubical Agda. Essentially, we prove the following:

> there exists a formal topology â„± such that the frame generated from â„±
> classifies the opens of any locale.

In Agda, we express this as follows:

```agda
sierpiÅ„ski-exists : Î£[ S âˆˆ FormalTopology ğ“¤â‚€ ğ“¤â‚€ ]
                     ((A : Frame ğ“¤â‚ ğ“¤â‚€ ğ“¤â‚€) â†’ (to-frame S â”€fâ†’ A) â‰ƒ âˆ£ A âˆ£F)
```

You can click [here](#15058) to jump directly to the inhabitant of this type
that we construct, and follow the construction in a top-down manner. Otherwise,
you can continue reading and follow in a bottom-up manner.

## SierpiÅ„ski formal topology

We start by writing down our poset of basic opens, which is the following
two element poset:

```text
        false
          |
          |
          |
        true
```

It is a bit counterintuitive that `true` is less than `false` but we are working
with opposites of the usual â€œinformation orderingâ€ posets from domain theory.

```agda
S-pos : (ğ“¤ ğ“¥ : Universe) â†’ Poset ğ“¤ ğ“¥
S-pos ğ“¤ ğ“¥ = Bool ğ“¤ , (_â‰¤_ , Bool-set , â‰¤-refl , â‰¤-trans , â‰¤-antisym)
  where
  _â‰¤_ : Bool ğ“¤ â†’ Bool ğ“¤ â†’ hProp ğ“¥
  _     â‰¤ false = Unit ğ“¥ , Unit-prop
  true  â‰¤ true  = Unit ğ“¥ , Unit-prop
  _     â‰¤ _     = bot ğ“¥

  â‰¤-refl : (x : Bool ğ“¤) â†’ [ x â‰¤ x ]
  â‰¤-refl false = tt
  â‰¤-refl true  = tt

  â‰¤-trans : (x y z : Bool ğ“¤) â†’ [ x â‰¤ y ] â†’ [ y â‰¤ z ] â†’ [ x â‰¤ z ]
  â‰¤-trans _ true true  p _ = p
  â‰¤-trans _ _    false _ _ = tt

  â‰¤-antisym : (x y : Bool ğ“¤) â†’ [ x â‰¤ y ] â†’ [ y â‰¤ x ] â†’ x â‰¡ y
  â‰¤-antisym false false p q = refl
  â‰¤-antisym true  true  p q = refl
```

The SierpiÅ„ski formal topology is obtained by equipping this poset with the
empty interaction system, which ensures that the inductively generated covering
relation is trivial.

```agda
S : (ğ“¤ ğ“¥ : Universe) â†’ FormalTopology ğ“¤ ğ“¥
S ğ“¤ ğ“¥ = S-pos ğ“¤ ğ“¥ , S-IS , S-has-mono , S-has-sim
  where
  S-exp : Bool ğ“¤ â†’ ğ“¤ Ì‡
  S-exp _ = ğŸ˜ ğ“¤

  S-out : {x : Bool ğ“¤} â†’ S-exp x â†’ ğ“¤ Ì‡
  S-out ()

  S-rev : {x : Bool ğ“¤} {y : S-exp x} â†’ S-out {x = x} y â†’ Bool ğ“¤
  S-rev {y = ()}

  S-IS : InteractionStr (Bool ğ“¤)
  S-IS = S-exp , (Î» {x} â†’ S-out {x = x}) , S-rev

  S-has-mono : hasMono (S-pos ğ“¤ ğ“¥) S-IS
  S-has-mono _ () _

  S-has-sim  : hasSimulation (S-pos ğ“¤ ğ“¥) S-IS
  S-has-sim _ _ _ ()
```

## The SierpiÅ„ski frame

The SierpÃ­nski frame ğ•Š is defined simply as `to-frame S`:

```agda
ğ•Š : Frame ğ“¤â‚ ğ“¤â‚€ ğ“¤â‚€
ğ•Š = to-frame (S ğ“¤â‚€ ğ“¤â‚€)
```

First of all, notice that the covering is trivial:

<!--
```agda
open NucleusFrom (S ğ“¤â‚€ ğ“¤â‚€)
```
-->

```agda
â—-triv : (U : ğ’« (Bool ğ“¤â‚€)) (b : Bool ğ“¤â‚€) â†’ b â— U â†’ [ b âˆˆ U ]
â—-triv U b (dir p)        = p
â—-triv U b (squash p q i) = isProp[] (b âˆˆ U) (â—-triv U b p) (â—-triv U b q) i
```

Let us write down the fact that equality in the SierpiÅ„ski frame reduces to
equality of the underlying sets:

```agda
ğ•Š-equality : (ğ”˜ ğ”™ : âˆ£ ğ•Š âˆ£F) â†’ â¦… ğ”˜ â¦† â‰¡ â¦… ğ”™ â¦† â†’ ğ”˜ â‰¡ ğ”™
ğ•Š-equality ğ”˜ ğ”™ p = Î£â‰¡Prop ntsâ‚€ (Î£â‰¡Prop ntsâ‚ p)
  where
  ntsâ‚€ : (U : âˆ£ DCPoset (S-pos _ _) âˆ£â‚š) â†’ isProp (ğ•› U â‰¡ U)
  ntsâ‚€ U = carrier-is-set (DCPoset (S-pos ğ“¤â‚€ ğ“¤â‚€)) (ğ•› U) U

  ntsâ‚ : (U : ğ’« âˆ£ S-pos ğ“¤â‚€ ğ“¤â‚€ âˆ£â‚š) â†’ isProp [ isDownwardsClosed (S-pos ğ“¤â‚€ ğ“¤â‚€) U ]
  ntsâ‚ U = isProp[] (isDownwardsClosed (S-pos ğ“¤â‚€ ğ“¤â‚€) U)
```

There are three inhabitants of the Sierpinski frame so let us write this down
to make things a bit more concrete.

The singleton set containing true:

```agda
â…trueâ† : âˆ£ ğ•Š âˆ£F
â…trueâ† = (Q , Q-dc) , fix
  where
  Q : ğ’« âˆ£ S-pos ğ“¤â‚€ ğ“¤â‚€ âˆ£â‚š
  Q x = (x â‰¡ true) , Bool-set x true

  Q-dc : [ isDownwardsClosed (S-pos ğ“¤â‚€ ğ“¤â‚€) Q ]
  Q-dc true  true  xâˆˆQ _ = xâˆˆQ
  Q-dc false true  xâˆˆQ _ = âŠ¥-rec (trueâ‰ false (sym xâˆˆQ))
  Q-dc false false xâˆˆQ _ = xâˆˆQ

  fix : ğ•› (Q , Q-dc) â‰¡ (Q , Q-dc)
  fix = Î£â‰¡Prop
          (isProp[] âˆ˜ isDownwardsClosed (S-pos _ _))
          (âŠ†-antisym (â—-triv Q) (Î» _ â†’ dir))
```

Note that this is the same thing as `Î· true` i.e. the set `_ â— â… true â†`:

```agda
âŠ¤-lemma : (ğ”˜ : âˆ£ ğ•Š âˆ£F) â†’ [ true âˆˆ â¦… ğ”˜ â¦† ] â†’ [ â…trueâ† âŠ‘[ pos ğ•Š ] ğ”˜ ]
âŠ¤-lemma ğ”˜ p true  q = p
âŠ¤-lemma ğ”˜ p false q = âŠ¥-rec (trueâ‰ false (sym q))

â…trueâ†=Î·-true : â…trueâ† â‰¡ Î· true
â…trueâ†=Î·-true = ğ•Š-equality _ _ (âŠ†-antisym (âŠ¤-lemma (Î· true) (dir tt)) goal)
  where
  goal : [ â¦… Î· true â¦† âŠ† â¦… â…trueâ† â¦† ]
  goal true (dir p)     = refl
  goal b (squash p q i) = isProp[] (b âˆˆ â¦… â…trueâ† â¦†) (goal b p) (goal b q) i
```

The top element `âŠ¤[ ğ•Š ]` which is the set containing both `true` and `false`. It
is the same thing as the downwards-closure of `Î· false`.

```agda
ğŸ=Î·-false : âŠ¤[ ğ•Š ] â‰¡ Î· false
ğŸ=Î·-false = ğ•Š-equality âŠ¤[ ğ•Š ] (Î· false) (âŠ†-antisym goal Î» _ _ â†’ tt) 
  where
  goal : [ â¦… âŠ¤[ ğ•Š ] â¦† âŠ† â¦… Î· false â¦† ]
  goal true  _ = Ï€â‚ (Ï€â‚€ (Î· false)) true _ (dir tt) tt
  goal false _ = dir (âŠ‘[ S-pos ğ“¤â‚€ ğ“¤â‚€ ]-refl false)
```

We will sometimes how to talk about set being non-empty i.e. containing either
`true` or `false`. To do that, we define the following function:

```agda
_â‰ âˆ… : (U : âˆ£ ğ•Š âˆ£F) â†’ ğ“¤â‚€ Ì‡
ğ”˜ â‰ âˆ… = [ true âˆˆ â¦… ğ”˜ â¦† ] âŠ [ false âˆˆ â¦… ğ”˜ â¦† ]
```

<!--
```agda
open import UniversalProperty

open import Cover
```
-->

## Is this the correct SierpiÅ„ski frame?

Fix a frame `A` whose index types are small.

```agda
module _ (A : Frame ğ“¤ ğ“¥ ğ“¤â‚€) where
```

We need to show that `ğ•Š` classifies the opens of `A`.

We will use the following shorthand for `A`'s operations:

```
  â‹_ : Fam ğ“¤â‚€ âˆ£ A âˆ£F â†’ âˆ£ A âˆ£F
  â‹ U = â‹[ A ] U

  _âˆ¨_ : âˆ£ A âˆ£F â†’ âˆ£ A âˆ£F â†’ âˆ£ A âˆ£F
  x âˆ¨ y = x âˆ¨[ A ] y

  _âˆ§_ : âˆ£ A âˆ£F â†’ âˆ£ A âˆ£F â†’ âˆ£ A âˆ£F
  x âˆ§ y = x âŠ“[ A ] y

  _â‰¤_ : âˆ£ A âˆ£F â†’ âˆ£ A âˆ£F â†’ hProp ğ“¥
  x â‰¤ y = x âŠ‘[ pos A ] y 

  ğŸ : âˆ£ A âˆ£F
  ğŸ = âŠ¤[ A ]
```

We now construct an isomorphism

```text
        to  :  (ğ•Š â”€fâ†’ A)  â‰ƒ  A  :  from
```

### The forwards direction (easy)

```agda
  to : (ğ•Š â”€fâ†’ A) â†’ âˆ£ A âˆ£F
  to ((f , _) , _) = f â…trueâ†
```

### The backwards direction

Let us first define an auxiliary function that we will need in the definition
of `from`, called `ğ”¨`, defined as:

```agda
  ğ”¨ : âˆ£ A âˆ£F â†’ âˆ£ ğ•Š âˆ£F â†’ Fam ğ“¤â‚€ âˆ£ A âˆ£F
  ğ”¨ x ğ”˜ = â… x âˆ£ _ âˆ¶ [ true âˆˆ â¦… ğ”˜ â¦† ] â† âˆªf â… ğŸ âˆ£ _ âˆ¶ [ false âˆˆ â¦… ğ”˜ â¦† ] â†
```

We then define the underlying function of `from` that we call `fromâ‚€`:

```agda
  fromâ‚€ : âˆ£ A âˆ£F â†’ âˆ£ ğ•Š âˆ£F â†’ âˆ£ A âˆ£F
  fromâ‚€ x ğ”˜ = â‹ ğ”¨ x ğ”˜
```

To be able to define `from`, which is supposed to be a frame homomorphism for
every `x : âˆ£ A âˆ£`, we need to show that `fromâ‚€`:

1. is monotonic,
1. respects finite meets, and
1. respects joins,

Let us start by proving some lemmas that we will need to prove those.

First, we note that applying `fromâ‚€` to `â…trueâ†` gives back `x`:

```agda
  from-lemmaâ‚€ : (x : âˆ£ A âˆ£F) â†’ fromâ‚€ x â…trueâ† â‰¡ x
  from-lemmaâ‚€ x = âŠ‘[ pos A ]-antisym _ _ ntsâ‚€ ntsâ‚
    where
    ntsâ‚€ : [ fromâ‚€ x â…trueâ† âŠ‘[ pos A ] x ]
    ntsâ‚€ = â‹[ A ]-least _ _ Î» { y (inl i , eq) â†’ â‰¡â‡’âŠ‘ (pos A) (sym eq)
                              ; y (inr i , eq) â†’ âŠ¥-rec (trueâ‰ false (sym i))
                              }

    ntsâ‚ : [ x â‰¤ fromâ‚€ x â…trueâ† ]
    ntsâ‚ = â‹[ A ]-upper _ _ (inl refl , refl)
```

`fromâ‚€` respects the top element:

```agda
  fromâ‚€-resp-âŠ¤ : (x : âˆ£ A âˆ£F) â†’ fromâ‚€ x âŠ¤[ ğ•Š ] â‰¡ âŠ¤[ A ]
  fromâ‚€-resp-âŠ¤ x =
    âŠ‘[ pos A ]-antisym _ _ (âŠ¤[ A ]-top _) (â‹[ A ]-upper _ _ (inr tt , refl))
```

It also respects the bottom element:

```agda
  from-resp-âŠ¥ : (x : âˆ£ A âˆ£F) â†’ fromâ‚€ x âŠ¥[ ğ•Š ] â‰¡ âŠ¥[ A ]
  from-resp-âŠ¥ x =
    âŠ‘[ pos A ]-antisym _ _ (â‹[ A ]-least _ _ nts) (âŠ¥[ A ]-bottom _)
    where
    nts : [ âˆ€[ z Îµ _ ] (z â‰¤ âŠ¥[ A ]) ]
    nts z (inl (dir p)        , eq) = âˆ¥âˆ¥-rec (isProp[] (_ â‰¤ _)) (Î» { (() , _) }) p
    nts z (inl (squash p q i) , eq) = isProp[] (_ â‰¤ _) (nts z (inl p , eq)) (nts z (inl q , eq)) i
    nts z (inr (dir p)        , eq) = âˆ¥âˆ¥-rec (isProp[] (_ â‰¤ _)) (Î» { (() , _) }) p
    nts z (inr (squash p q i) , eq) = isProp[] (_ â‰¤ _) (nts z (inr p , eq)) (nts z (inr q , eq)) i
```

If some `ğ”˜ : ğ•Š` contains `false`, then it has to be the entire subset:

```agda
  falseâˆˆğ”˜â†’ğ”˜-top : (ğ”˜ : âˆ£ ğ•Š âˆ£F) â†’ [ false âˆˆ â¦… ğ”˜ â¦† ] â†’ âŠ¤[ ğ•Š ] â‰¡ ğ”˜
  falseâˆˆğ”˜â†’ğ”˜-top ğ”˜ falseâˆˆğ”˜ = ğ•Š-equality âŠ¤[ ğ•Š ] ğ”˜ (sym (goal ğ”˜ falseâˆˆğ”˜))
    where
    goal : (ğ”˜ : âˆ£ ğ•Š âˆ£F) â†’ [ false âˆˆ â¦… ğ”˜ â¦† ] â†’ â¦… ğ”˜ â¦† â‰¡ entire
    goal ((U , U-dc) , _) falseâˆˆğ”˜ = âŠ†-antisym ntsâ‚€ ntsâ‚
      where
      ntsâ‚€ : [ U âŠ† entire ]
      ntsâ‚€ _ _ = tt

      ntsâ‚ : [ entire âŠ† U ]
      ntsâ‚ true  tt = U-dc false true falseâˆˆğ”˜ tt
      ntsâ‚ false tt = falseâˆˆğ”˜
```

#### Monotonicity

Monotonicity of `fromâ‚€` is easy to show.

```agda
  fromâ‚€-mono : (x : âˆ£ A âˆ£F) â†’ isMonotonic (pos ğ•Š) (pos A) (fromâ‚€ x)
  fromâ‚€-mono x ğ”˜ ğ”™ ğ”˜âŠ†ğ”™ = â‹[ A ]-least _ _ nts
    where
    nts : _
    nts _ (inl i , eq) = â‹[ A ]-upper _ _ (inl (ğ”˜âŠ†ğ”™ true  i) , eq)
    nts _ (inr j , eq) = â‹[ A ]-upper _ _ (inr (ğ”˜âŠ†ğ”™ false j) , eq)
```

```agda
  fromâ‚€m : âˆ£ A âˆ£F â†’ pos ğ•Š â”€mâ†’ pos A
  fromâ‚€m x = fromâ‚€ x , fromâ‚€-mono x
```

#### Continuity

We now prove that `fromâ‚€` is a frame homomorphism.

We have already shown that it respects the top element.

```agda
  resp-âŠ¤ : (x : âˆ£ A âˆ£F) â†’ fromâ‚€ x âŠ¤[ ğ•Š ] â‰¡ âŠ¤[ A ]
  resp-âŠ¤ = fromâ‚€-resp-âŠ¤
```

To show meet preservation, we make use of the fact that bi-cofinal families have
the same join.

```agda
  fromâ‚€-resp-âˆ§ : (x : âˆ£ A âˆ£F) (ğ”˜ ğ”™ : âˆ£ ğ•Š âˆ£F)
               â†’ fromâ‚€ x (ğ”˜ âŠ“[ ğ•Š ] ğ”™) â‰¡ fromâ‚€ x ğ”˜ âˆ§ fromâ‚€ x ğ”™
  fromâ‚€-resp-âˆ§ x ğ”˜@((_ , ğ”˜-dc) , _) ğ”™@((_ , ğ”™-dc) , _) =
    fromâ‚€ x (ğ”˜ âŠ“[ ğ•Š ] ğ”™)                    â‰¡âŸ¨ refl                  âŸ©
    â‹ ğ”¨ x (ğ”˜ âŠ“[ ğ•Š ] ğ”™)                      â‰¡âŸ¨ nts                   âŸ©
    (â‹ â… _ âˆ§ _ âˆ£ (_ , _) âˆ¶ ğ”˜ â‰ âˆ… Ã— ğ”™ â‰ âˆ… â† )  â‰¡âŸ¨ sym (sym-distr A _ _) âŸ©
    (â‹ ğ”¨ x ğ”˜) âˆ§ (â‹ ğ”¨ x ğ”™)                   â‰¡âŸ¨ refl                  âŸ©
    (fromâ‚€ x ğ”˜) âˆ§ (fromâ‚€ x ğ”™)               âˆ
    where
    ntsâ‚€ : _
    ntsâ‚€ (inl (p , q)) = (inl p , inl q) , â‰¡â‡’âŠ‘ (pos A) (sym (xâˆ§x=x A x))
    ntsâ‚€ (inr (p , q)) = (inr p , inr q) , â‰¡â‡’âŠ‘ (pos A) (sym (xâˆ§x=x A âŠ¤[ A ]))

    ntsâ‚ : _
    ntsâ‚ (inl p , inl q) = inl (p , q) , â‰¡â‡’âŠ‘ (pos A) (xâˆ§x=x A x)
    ntsâ‚ (inl p , inr q) = inl (p , ğ”™-dc false true q tt) , âŠ“[ A ]-lowerâ‚€ _ _
    ntsâ‚ (inr p , inl q) = inl (ğ”˜-dc false true p tt , q) , âŠ“[ A ]-lowerâ‚ _ _
    ntsâ‚ (inr p , inr q) = (inr (p , q)) , â‰¡â‡’âŠ‘ (pos A) (xâˆ§x=x A âŠ¤[ A ])

    nts : (â‹ _) â‰¡ (â‹ _)
    nts = bicofinalâ†’same-join A _ _ (ntsâ‚€ , ntsâ‚)
```

Preservation of joins is a bit more complicated. Let `W` be a family of
SierpiÅ„ski opens and let `x : A`. We use the uniqueness of
`â‹ â… fromâ‚€ x ğ”˜ âˆ£ ğ”˜ Îµ W â†` by showing that `fromâ‚€ x (â‹[ ğ•Š ] W)` is the least
upper bound of `â… fromâ‚€ x ğ”˜ âˆ£ ğ”˜ Îµ W â†`. The fact that it is an upper bound
is given by `ub` and the fact that it is the least such is given in `least`.

```agda
  fromâ‚€-comm-â‹ : (x : âˆ£ A âˆ£F) (W : Fam ğ“¤â‚€ âˆ£ ğ•Š âˆ£F)
               â†’ fromâ‚€ x (â‹[ ğ•Š ] W) â‰¡ â‹ â… fromâ‚€ x ğ”˜ âˆ£ ğ”˜ Îµ W â†
  fromâ‚€-comm-â‹ x W = â‹-unique A _ _ ub least
    where
    ub : [ âˆ€[ z Îµ â… fromâ‚€ x ğ”˜ âˆ£ ğ”˜ Îµ W â† ] (z â‰¤ fromâ‚€ x (â‹[ ğ•Š ] W)) ]
    ub z (i , eq) =
      subst (Î» - â†’ [ - â‰¤ _ ]) eq (fromâ‚€-mono x (W $ i) (â‹[ ğ•Š ] W) rem)
      where
      rem : [ (W $ i) âŠ‘[ pos ğ•Š ] (â‹[ ğ•Š ] W) ]
      rem _ xâˆˆWáµ¢ = dir âˆ£ i , xâˆˆWáµ¢ âˆ£

    least : (u : âˆ£ A âˆ£F)
          â†’ (((z : âˆ£ A âˆ£F) â†’ z Îµ â… fromâ‚€ x ğ”˜ âˆ£ ğ”˜ Îµ W â† â†’ [ z â‰¤ u ]))
          â†’ [ fromâ‚€ x (â‹[ ğ•Š ] W) â‰¤ u ]
    least u u-upper = â‹[ A ]-least _ _ rem
      where
      open PosetReasoning (pos A)

      rem : (z : âˆ£ A âˆ£F) â†’ z Îµ ğ”¨ x (â‹[ ğ•Š ] W) â†’ [ z â‰¤ u ]
      rem z (inl (dir p) , eq) =
        subst (Î» - â†’ [ - â‰¤ u ]) eq (âˆ¥âˆ¥-rec (isProp[] (_ â‰¤ _ )) goal p)
        where
        goal : _
        goal (j , trueâˆˆWâ±¼) =
          x                âŠ‘âŸ¨ â‰¡â‡’âŠ‘ (pos A) (sym (from-lemmaâ‚€ x))    âŸ©
          fromâ‚€ x â…trueâ†   âŠ‘âŸ¨ fromâ‚€-mono x â…trueâ† (W $ j) nts      âŸ©
          fromâ‚€ x (W $ j)  âŠ‘âŸ¨ u-upper (fromâ‚€ x (W $ j)) (j , refl) âŸ©
          u                â– 
          where
          nts : [ â…trueâ† âŠ‘[ pos ğ•Š ] (W $ j) ]
          nts true  _ = trueâˆˆWâ±¼
          nts false p = âŠ¥-rec (trueâ‰ false (sym p))
      rem z (inr (dir p) , eq) =
        subst (Î» - â†’ [ - â‰¤ u ]) eq (âˆ¥âˆ¥-rec (isProp[] (_ â‰¤ _)) goal p)
        where
        goal : _
        goal (j , falseâˆˆWâ±¼) = u-upper ğŸ (j , nts)
          where
          Wâ±¼=âŠ¤ : W $ j â‰¡ âŠ¤[ ğ•Š ]
          Wâ±¼=âŠ¤ = sym (falseâˆˆğ”˜â†’ğ”˜-top (W $ j) falseâˆˆWâ±¼)

          nts : fromâ‚€ x (W $ j) â‰¡ ğŸ
          nts = fromâ‚€ x (W $ j) â‰¡âŸ¨ cong (fromâ‚€ x) Wâ±¼=âŠ¤ âŸ©
                fromâ‚€ x âŠ¤[ ğ•Š ]  â‰¡âŸ¨ resp-âŠ¤ x            âŸ©
                ğŸ               âˆ
      rem z (inl (squash p q i) , eq) =
        isProp[] (_ â‰¤ _) (rem z (inl p , eq)) (rem z (inl q , eq)) i
      rem z (inr (squash p q i) , eq) =
        isProp[] (_ â‰¤ _) (rem z (inr p , eq)) (rem z (inr q , eq)) i
```

The combination of these two proofs give us the fact that `fromâ‚€` is a frame
homomorphism (i.e. a continuous function):

```agda
  fromâ‚€-cont : (x : âˆ£ A âˆ£F) â†’ isFrameHomomorphism ğ•Š A (fromâ‚€m x)
  fromâ‚€-cont x = resp-âŠ¤ x , fromâ‚€-resp-âˆ§ x  , fromâ‚€-comm-â‹ x
```

We are now ready to write down `from`:

```agda
  from : âˆ£ A âˆ£F â†’ ğ•Š â”€fâ†’ A
  Ï€â‚€ (Ï€â‚€ (from x)) = fromâ‚€ x
  Ï€â‚ (Ï€â‚€ (from x)) = fromâ‚€-mono x
  Ï€â‚ (from x)      = fromâ‚€-cont x
```

#### `to` cancels `from`

```agda
  toâˆ˜from=id : (x : âˆ£ A âˆ£F) â†’ to (from x) â‰¡ x
  toâˆ˜from=id x = to (from x) â‰¡âŸ¨ refl âŸ© fromâ‚€ x â…trueâ† â‰¡âŸ¨ from-lemmaâ‚€ x âŸ© x âˆ
```

#### `from` cancels `to`

To prove this result, we will make use of the following, rather important
lemma:

```agda
  useful-lemma : (((f , _) , _) : ğ•Š â”€fâ†’ A) (ğ”˜ : âˆ£ ğ•Š âˆ£F)
            â†’ â‹ â… f (Î· u) âˆ£ u âˆˆ â¦… ğ”˜ â¦† â† â‰¡ f ğ”˜
  useful-lemma ğ’»@((f , _) , _ , _ , f-resp-â‹) ğ”˜ =
    â‹ â… f (Î· u) âˆ£ u âˆˆ â¦… ğ”˜ â¦† â†      â‰¡âŸ¨ sym (f-resp-â‹ (â… Î· u âˆ£ u âˆˆ â¦… ğ”˜ â¦† â†))   âŸ©
    f (â‹[ ğ•Š ] â… Î· u âˆ£ u âˆˆ â¦… ğ”˜ â¦† â†) â‰¡âŸ¨ sym (cong f (main-lemma (S ğ“¤â‚€ ğ“¤â‚€) ğ”˜))  âŸ©
    f ğ”˜                            âˆ
```

We now prove that `from` cancels `to`:

```agda
  fromâˆ˜to=id : (ğ’» : ğ•Š â”€fâ†’ A) â†’ from (to ğ’») â‰¡ ğ’» 
  fromâˆ˜to=id ğ’»@((f , f-mono) , f-resp-âŠ¤ , _) =
    forget-homo ğ•Š A (from (to ğ’»)) ğ’» goal
    where
    goal : (ğ”˜ : âˆ£ ğ•Š âˆ£F) â†’ from (to ğ’») .Ï€â‚€ .Ï€â‚€ ğ”˜ â‰¡ f ğ”˜
    goal ğ”˜ = sym (â‹-unique A _ _ ub least)
      where
      open PosetReasoning (pos A)

      ub : (x : âˆ£ A âˆ£F) â†’ x Îµ ğ”¨ (to ğ’») ğ”˜ â†’ [ x â‰¤ (f ğ”˜) ]
      ub x (inl i , eq) = subst (Î» - â†’ [ - â‰¤ f ğ”˜ ]) eq nts
        where
        â¦…ğŸâ¦† : [ f â…trueâ† â‰¤ f ğ”˜ ]
        â¦…ğŸâ¦† = f-mono _ _ (âŠ¤-lemma ğ”˜ i) 

        nts : [ (ğ”¨ (f â…trueâ†) ğ”˜ $ inl i) â‰¤ f ğ”˜ ]
        nts = â… f â…trueâ† âˆ£ _ âˆ¶ [ true âˆˆ â¦… ğ”˜ â¦† ] â† $ i âŠ‘âŸ¨ â‰¡â‡’âŠ‘ (pos A) refl âŸ©
              f â…trueâ†                                âŠ‘âŸ¨ â¦…ğŸâ¦†              âŸ©
              f ğ”˜                                     â– 
      ub x (inr j , eq) = subst (Î» - â†’ [ - â‰¤ f ğ”˜ ]) eq (â‰¡â‡’âŠ‘ (pos A) nts)
        where
        nts : ğ”¨ (to ğ’») ğ”˜ $ inr j â‰¡ f ğ”˜
        nts = ğ”¨ (to ğ’») ğ”˜ $ inr j â‰¡âŸ¨ refl                       âŸ©
              âŠ¤[ A ]             â‰¡âŸ¨ sym f-resp-âŠ¤               âŸ©
              f âŠ¤[ ğ•Š ]           â‰¡âŸ¨ cong f (falseâˆˆğ”˜â†’ğ”˜-top ğ”˜ j) âŸ©
              f ğ”˜                âˆ

      least : (u : âˆ£ A âˆ£F)
           â†’ ((x : âˆ£ A âˆ£F) â†’ x Îµ ğ”¨ (to ğ’») ğ”˜ â†’ [ x â‰¤ u ]) â†’ [ f ğ”˜ â‰¤ u ]
      least u p =
        subst (Î» - â†’ [ - â‰¤ u ]) (useful-lemma ğ’» ğ”˜) (â‹[ A ]-least _ _ rem)
        where
        Ï€ : [ false âˆˆ â¦… ğ”˜ â¦† ] â†’ [ âŠ¤[ A ] â‰¤ u ]
        Ï€ q = p âŠ¤[ A ] (inr q , refl)

        Ï : [ true âˆˆ â¦… ğ”˜ â¦† ] â†’ [ f â…trueâ† â‰¤ u ]
        Ï q = p (f â…trueâ†) (inl q , refl)

        rem : [ âˆ€[ z Îµ â… f (Î· u) âˆ£ u âˆˆ â¦… ğ”˜ â¦† â† ] (z â‰¤ u) ]
        rem z ((true  , q) , eq) = subst (Î» - â†’ [ - â‰¤ u ]) eq nts
          where
          nts = f (Î· true) âŠ‘âŸ¨ â‰¡â‡’âŠ‘ (pos A) (cong f (sym â…trueâ†=Î·-true)) âŸ©
                f â…trueâ†   âŠ‘âŸ¨ Ï q                                      âŸ©
                u          â– 
        rem z ((false , q) , eq) = subst (Î» - â†’ [ - â‰¤ u ]) eq nts
          where
          nts = f (Î· false) âŠ‘âŸ¨ â‰¡â‡’âŠ‘ (pos A) (cong f (sym ğŸ=Î·-false)) âŸ©
                f âŠ¤[ ğ•Š ]    âŠ‘âŸ¨ â‰¡â‡’âŠ‘ (pos A) f-resp-âŠ¤                 âŸ©
                âŠ¤[ A ]      âŠ‘âŸ¨ Ï€ q                                  âŸ©
                u           â– 
```

Finally, we write down the desired equivalence:

```agda
  ğ•Š-correct : (ğ•Š â”€fâ†’ A) â‰ƒ âˆ£ A âˆ£F
  ğ•Š-correct = isoToEquiv (iso to from toâˆ˜from=id fromâˆ˜to=id)
```

```agda
main-proof        = S ğ“¤â‚€ ğ“¤â‚€ , ğ•Š-correct
sierpiÅ„ski-exists = main-proof
```
