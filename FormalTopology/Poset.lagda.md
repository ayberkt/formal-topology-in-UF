```
{-# OPTIONS --without-K --cubical --safe #-}

module Poset where

open import Basis
open import Cubical.Foundations.SIP
open import Cubical.Structures.Axioms
open import Cubical.Foundations.Equiv using (_â‰ƒâŸ¨_âŸ©_) renaming (_â–  to _ğ””ğ”ˆğ”‡)
```

## Definition of poset

```agda
Order : (â„“â‚ : Level) â†’ Type â„“ â†’ Type (â„“-max â„“ (â„“-suc â„“â‚))
Order â„“â‚ A = A â†’ A â†’ hProp â„“â‚

Order-set : (â„“â‚ : Level) (A : Type â„“â‚€) â†’ isSet (Order â„“â‚ A)
Order-set â„“â‚ A = isSetÎ 2 Î» _ _ â†’ isSetHProp

isReflexive : {A : Type â„“â‚€} â†’ Order â„“â‚ A â†’ hProp (â„“-max â„“â‚€ â„“â‚)
isReflexive {A = X} _âŠ‘_ =
  ((x : X) â†’ [ x âŠ‘ x ]) , isPropÎ  (Î» x â†’ is-true-prop (x âŠ‘ x))

isTransitive : {A : Type â„“â‚€} â†’ Order â„“â‚ A â†’ hProp (â„“-max â„“â‚€ â„“â‚)
isTransitive {â„“â‚€ = â„“â‚€} {â„“â‚ = â„“â‚} {A = X} _âŠ‘_ = âŠ‘-trans , âŠ‘-trans-prop
  where
    âŠ‘-trans : Type (â„“-max â„“â‚€ â„“â‚)
    âŠ‘-trans = ((x y z : X) â†’ [ x âŠ‘ y â‡’ y âŠ‘ z â‡’ x âŠ‘ z ])

    âŠ‘-trans-prop : isProp  âŠ‘-trans
    âŠ‘-trans-prop = isPropÎ 3 Î» x y z â†’ is-true-prop (x âŠ‘ y â‡’ y âŠ‘ z â‡’ x âŠ‘ z)

isAntisym : {A : Type â„“â‚€} â†’ isSet A â†’ Order â„“â‚ A â†’ hProp (â„“-max â„“â‚€ â„“â‚)
isAntisym {A = A} A-set _âŠ‘_ = âŠ‘-antisym , âŠ‘-antisym-prop
  where
    âŠ‘-antisym = (x y : A) â†’ [ x âŠ‘ y ] â†’ [ y âŠ‘ x ] â†’ x â‰¡ y

    âŠ‘-antisym-prop : isProp âŠ‘-antisym
    âŠ‘-antisym-prop = isPropÎ 2 Î» x y â†’ isPropÎ 2 Î» _ _ â†’ A-set x y

PosetAx : (A : Type â„“â‚€) â†’ Order â„“â‚ A â†’ hProp (â„“-max â„“â‚€ â„“â‚)
PosetAx {â„“â‚€ = â„“â‚€} {â„“â‚ = â„“â‚} A _âŠ‘_ = isAPartialSet , isAPartialSet-prop
  where
    isAPartialSet =
      Î£[ A-set âˆˆ isSet A ] [ isReflexive _âŠ‘_ âŠ“ isTransitive _âŠ‘_ âŠ“ isAntisym A-set _âŠ‘_ ]

    isAPartialSet-prop =
      isPropÎ£ isPropIsSet Î» A-set â†’
        is-true-prop (isReflexive _âŠ‘_ âŠ“ isTransitive _âŠ‘_ âŠ“ isAntisym A-set _âŠ‘_)
```

A poset structure with level `â„“â‚`.

```agda
PosetStr : (â„“â‚ : Level) â†’ Type â„“ â†’ Type (â„“-max â„“ (â„“-suc â„“â‚))
PosetStr â„“â‚ A = Î£[ âŠ‘ âˆˆ Order â„“â‚ A ] [ PosetAx A âŠ‘ ]

PosetStr-set : (â„“â‚ : Level) (A : Type â„“â‚€) â†’ isSet (PosetStr â„“â‚ A)
PosetStr-set â„“â‚ A =
  isSetÎ£ (isSetÎ  Î» _ â†’ isSetÎ  Î» _ â†’ isSetHProp) Î» _âŠ‘_ â†’
  isSetÎ£ (isPropâ†’isSet isPropIsSet) Î» A-set â†’
    isPropâ†’isSet (is-true-prop (isReflexive _âŠ‘_ âŠ“ isTransitive _âŠ‘_ âŠ“ isAntisym A-set _âŠ‘_))
```

A poset with carrier level `â„“â‚€` and relation level `â„“â‚`.

```agda
Poset : (â„“â‚€ â„“â‚ : Level) â†’ Type (â„“-max (â„“-suc â„“â‚€) (â„“-suc â„“â‚))
Poset â„“â‚€ â„“â‚ = Î£ (Type â„“â‚€) (PosetStr â„“â‚)
```

## Projections

Given a poset `P`, `âˆ£ P âˆ£â‚š` denotes its carrier set and `strâ‚š P` its order structure.

```agda
âˆ£_âˆ£â‚š : Poset â„“â‚€ â„“â‚ â†’ Type â„“â‚€
âˆ£ X , _ âˆ£â‚š = X

strâ‚š : (P : Poset â„“â‚€ â„“â‚) â†’ PosetStr â„“â‚ âˆ£ P âˆ£â‚š
strâ‚š (_ , s) = s
```

We refer to to the order of `P` as `_âŠ‘[ P ]_`.

```agda
rel : (P : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â†’ âˆ£ P âˆ£â‚š â†’ hProp â„“â‚
rel (_ , _âŠ‘_ , _) = _âŠ‘_

infix 9 rel

syntax rel P x y = x âŠ‘[ P ] y

relâ‚‚ : (P : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â†’ âˆ£ P âˆ£â‚š â†’ âˆ£ P âˆ£â‚š â†’ hProp â„“â‚
relâ‚‚ P x y z = (x âŠ‘[ P ] z) âŠ“ (y âŠ‘[ P ] z)

syntax relâ‚‚ P x y z = âŸ¨ x , y âŸ©âŠ‘[ P ] z

reláµ’áµ– : (P : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â†’ âˆ£ P âˆ£â‚š â†’ hProp â„“â‚
reláµ’áµ– (_ , _âŠ‘_ , _) x y = y âŠ‘ x

syntax reláµ’áµ– P x y = x âŠ’[ P ] y
```

Similarly, we define projections for the poset properties.

```agda
âŠ‘[_]-refl : (P : Poset â„“â‚€ â„“â‚) â†’ (x : âˆ£ P âˆ£â‚š) â†’ [ x âŠ‘[ P ] x ]
âŠ‘[_]-refl (_ , _ , _ , âŠ‘-refl , _) = âŠ‘-refl

âŠ‘[_]-trans : (P : Poset â„“â‚€ â„“â‚) (x y z : âˆ£ P âˆ£â‚š)
           â†’ [ x âŠ‘[ P ] y ] â†’ [ y âŠ‘[ P ] z ] â†’ [ x âŠ‘[ P ] z ]
âŠ‘[_]-trans (_ , _ , _ , _ , âŠ‘-trans , _) = âŠ‘-trans

âŠ‘[_]-antisym : (P : Poset â„“â‚€ â„“â‚) (x y : âˆ£ P âˆ£â‚š)
             â†’ [ x âŠ‘[ P ] y ] â†’ [ y âŠ‘[ P ] x ] â†’ x â‰¡ y
âŠ‘[_]-antisym (_ , _ , _ , _ , _ , âŠ‘-antisym) = âŠ‘-antisym

carrier-is-set : (P : Poset â„“â‚€ â„“â‚) â†’ isSet âˆ£ P âˆ£â‚š
carrier-is-set (_ , _ , is-set , _) = is-set
```

## Partial order reasoning

```agda
module PosetNotation (P : Poset ğ“¤ ğ“¥) where

  _â‰¤_ : âˆ£ P âˆ£â‚š â†’ âˆ£ P âˆ£â‚š â†’ hProp ğ“¥
  x â‰¤ y = x âŠ‘[ P ] y
```

Some convenient notation for carrying out inequality reasoning.

```agda
module PosetReasoning (P : Poset â„“â‚€ â„“â‚) where

  _âŠ‘âŸ¨_âŸ©_ : (x : âˆ£ P âˆ£â‚š) {y z : âˆ£ P âˆ£â‚š}
         â†’ [ x âŠ‘[ P ] y ] â†’ [ y âŠ‘[ P ] z ] â†’ [ x âŠ‘[ P ] z ]
  _ âŠ‘âŸ¨ p âŸ© q = âŠ‘[ P ]-trans _ _ _ p q

  _â–  : (x : âˆ£ P âˆ£â‚š) â†’ [ x âŠ‘[ P ] x ]
  _â–  = âŠ‘[ P ]-refl

  infixr 0 _âŠ‘âŸ¨_âŸ©_
  infix  1 _â– 
```

It is not convenient to have to keep applying `subst` for the show that two equal things
are below each other so let us make note of the following trivial fact.

```agda
â‰¡â‡’âŠ‘ : (P : Poset â„“â‚€ â„“â‚) â†’ {x y : âˆ£ P âˆ£â‚š} â†’ x â‰¡ y â†’ [ x âŠ‘[ P ] y ]
â‰¡â‡’âŠ‘ P {x = x} p = subst (Î» z â†’ [ x âŠ‘[ P ] z ]) p (âŠ‘[ P ]-refl x)
```

## Monotonic functions

We can define the notion preserving the order of a order structure for all types with
orders.

```agda
isOrderPreserving : (M : Î£ (Type â„“â‚€) (Order â„“â‚)) (N : Î£ (Type â„“â‚€â€²) (Order â„“â‚â€²))
                  â†’ (Ï€â‚€ M â†’ Ï€â‚€ N) â†’ Type _
isOrderPreserving (A , _âŠ‘â‚€_) (B , _âŠ‘â‚_) f = (x y : A) â†’ [ x âŠ‘â‚€ y ] â†’ [ f x âŠ‘â‚ f y ]
```

Technically, this is called "monotonic" as well but we will reserve that term for posets.

```agda
isMonotonic : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚€â€² â„“â‚â€²)
            â†’ (âˆ£ P âˆ£â‚š â†’ âˆ£ Q âˆ£â‚š) â†’ Type _
isMonotonic (A , (_âŠ‘â‚€_ , _)) (B , (_âŠ‘â‚_ , _)) = isOrderPreserving (A , _âŠ‘â‚€_) (B , _âŠ‘â‚_)
```

Both of these notions are propositional.

```agda
isOrderPreserving-prop : (M : Î£ (Type â„“â‚€) (Order â„“â‚)) (N : Î£ (Type â„“â‚€â€²) (Order â„“â‚â€²))
                         (f : Ï€â‚€ M â†’ Ï€â‚€ N)
                       â†’ isProp (isOrderPreserving M N f)
isOrderPreserving-prop M (_ , _âŠ‘â‚_) f = isPropÎ 3 Î» x y p â†’ is-true-prop ((f x) âŠ‘â‚ (f y))

isMonotonic-prop : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚€â€² â„“â‚â€²) (f : âˆ£ P âˆ£â‚š â†’ âˆ£ Q âˆ£â‚š)
                 â†’ isProp (isMonotonic P Q f)
isMonotonic-prop (A , (_âŠ‘â‚€_ , _)) (B , (_âŠ‘â‚_ , _)) f =
  isOrderPreserving-prop (A , _âŠ‘â‚€_) (B , _âŠ‘â‚_) f
```

We then collect monotonic functions in the following type.

```agda
_â”€mâ†’_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€â€² â„“â‚â€² â†’ Type _
_â”€mâ†’_ P Q = Î£ (âˆ£ P âˆ£â‚š â†’ âˆ£ Q âˆ£â‚š) (isMonotonic P Q)
```

Projection for the underlying function of a monotonic map.

```agda
_$â‚˜_ = Ï€â‚€
```

The identity monotonic map and composition of monotonic maps.

```agda
ğŸm : (P : Poset â„“â‚€ â„“â‚) â†’ P â”€mâ†’ P
ğŸm P = id âˆ£ P âˆ£â‚š , (Î» x y xâŠ‘y â†’ xâŠ‘y)

_âˆ˜m_ : {P : Poset â„“â‚€ â„“â‚} {Q : Poset â„“â‚€â€² â„“â‚â€²} {R : Poset â„“â‚€â€²â€² â„“â‚â€²â€²}
     â†’ (Q â”€mâ†’ R) â†’ (P â”€mâ†’ Q) â†’ (P â”€mâ†’ R)
(g , pg) âˆ˜m (f , pf) = g âˆ˜ f , Î» x y p â†’ pg (f x) (f y) (pf x y p)
```


We will often deal with the task of showing the equality of two monotonic functions. As
being monotonic is propositional, we can quickly reduce this to showing the equality of
the underlying functions using `Î£Propâ‰¡` but it is more convenient to record this fact in
advance.

```agda
forget-mono : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚€â€² â„“â‚â€²) ((f , f-mono) (g , g-mono) : P â”€mâ†’ Q)
            â†’ f â‰¡ g
            â†’ (f , f-mono) â‰¡ (g , g-mono)
forget-mono P Q (f , f-mono) (g , g-mono) =
  Î£â‰¡Prop (Î» f â†’ isPropÎ 3 Î» x y xâŠ‘y â†’ is-true-prop (f x âŠ‘[ Q ] f y))
```

## Downward-closure

We denote by `â†“[ P ] x` the type of everything in `P` that is below `x`.

```agda
â†“[_]_ : (P : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â†’ Type _
â†“[ P ] a = Î£[ b âˆˆ âˆ£ P âˆ£â‚š ] [ b âŠ‘[ P ] a ]
```

```agda
isDownwardsClosed : (P : Poset â„“â‚€ â„“â‚) â†’ ğ’« âˆ£ P âˆ£â‚š â†’ hProp _
isDownwardsClosed P U =
  ((x y : âˆ£ P âˆ£â‚š) â†’ [ x âˆˆ U ] â†’ [ y âŠ‘[ P ] x ] â†’ [ y âˆˆ U ]) , prop
  where
    prop : isProp ((x y : âˆ£ P âˆ£â‚š) â†’ [ U x ] â†’ [ y âŠ‘[ P ] x ] â†’ [ U y ])
    prop = isPropÎ  Î» _ â†’ isPropÎ  Î» x â†’ isPropÎ  Î» _ â†’ isPropÎ  Î» _ â†’ is-true-prop (x âˆˆ U)

DCSubset : (P : Poset â„“â‚€ â„“â‚) â†’ Type _
DCSubset P = Î£[ U âˆˆ ğ’« âˆ£ P âˆ£â‚š ] [ isDownwardsClosed P U ]

DCSubset-set : (P : Poset â„“â‚€ â„“â‚) â†’ isSet (DCSubset P)
DCSubset-set P =
  isSetÎ£ (ğ’«-set âˆ£ P âˆ£â‚š) Î» U â†’ isPropâ†’isSet (is-true-prop (isDownwardsClosed P U))
```

## Directedness

The notion of a *directed subset*, manifested here as a directed *family*.

```agda
isDirected : {â„“â‚‚ : Level} â†’ (P : Poset â„“â‚€ â„“â‚) â†’ Fam â„“â‚‚ âˆ£ P âˆ£â‚š â†’ hProp (â„“-max â„“â‚ â„“â‚‚)
isDirected P U@(I , _) =
  U-inhabited âŠ“ (âˆ€[ i âˆ¶ I ] âˆ€[ j âˆ¶ I ] âˆ¥ Î£[ k âˆˆ I ] [ âŸ¨ (U $ i) , (U $ j) âŸ©âŠ‘[ P ] (U $ k) ] âˆ¥ , âˆ¥âˆ¥-prop _)
  where
    U-inhabited : hProp _
    U-inhabited = âˆ¥ index U âˆ¥ , (âˆ¥âˆ¥-prop I)
```

## Product of two posets

```agda
_Ã—â‚š_ : (P : Poset â„“â‚€ â„“â‚) (Q : Poset â„“â‚€â€² â„“â‚â€²) â†’ Poset _ _
P Ã—â‚š Q = (âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) , _âŠ‘_ , carrier-set , (âŠ‘-refl , âŠ‘-trans , âŠ‘-antisym)
  where
    _âŠ‘_ : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š â†’ âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š â†’ hProp _
    _âŠ‘_ (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) = xâ‚€ âŠ‘[ P ] xâ‚ âŠ“ yâ‚€ âŠ‘[ Q ] yâ‚

    carrier-set : isSet (âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š)
    carrier-set = isSetÎ£ (carrier-is-set P) Î» _ â†’ (carrier-is-set Q)

    âŠ‘-refl : (p : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) â†’ [ p âŠ‘ p ]
    âŠ‘-refl (x , y) = (âŠ‘[ P ]-refl x) , (âŠ‘[ Q ]-refl y)

    âŠ‘-trans : (p q r : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) â†’ [ p âŠ‘ q ] â†’ [ q âŠ‘ r ] â†’ [ p âŠ‘ r ]
    âŠ‘-trans (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) (xâ‚‚ , yâ‚‚) (xâ‚€âŠ‘xâ‚ , yâ‚€âŠ‘yâ‚) (xâ‚âŠ‘xâ‚‚ , yâ‚âŠ‘yâ‚‚) =
      âŠ‘[ P ]-trans _ _ _ xâ‚€âŠ‘xâ‚ xâ‚âŠ‘xâ‚‚ , âŠ‘[ Q ]-trans _ _ _ yâ‚€âŠ‘yâ‚ yâ‚âŠ‘yâ‚‚

    âŠ‘-antisym : (p q : âˆ£ P âˆ£â‚š Ã— âˆ£ Q âˆ£â‚š) â†’ [ p âŠ‘ q ] â†’ [ q âŠ‘ p ] â†’ p â‰¡ q
    âŠ‘-antisym (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) (xâ‚€âŠ‘xâ‚ , yâ‚€âŠ‘yâ‚) (xâ‚âŠ‘xâ‚€ , yâ‚âŠ‘yâ‚€) =
      Î£PathTransportâ†’PathÎ£ (xâ‚€ , yâ‚€) (xâ‚ , yâ‚) (âŠ‘[ P ]-antisym _ _ xâ‚€âŠ‘xâ‚ xâ‚âŠ‘xâ‚€ , sym NTS)
      where
        NTS : yâ‚ â‰¡ transport refl yâ‚€
        NTS = subst (_â‰¡_ yâ‚) (sym (transportRefl yâ‚€)) (âŠ‘[ Q ]-antisym _ _ yâ‚âŠ‘yâ‚€ yâ‚€âŠ‘yâ‚)
```

The *diagonal* monotonic map.

```agda
Î” : (P : Poset â„“â‚€ â„“â‚) â†’ P â”€mâ†’ (P Ã—â‚š P)
Î” P = f , f-mono
  where
    f : âˆ£ P âˆ£â‚š â†’ âˆ£ P Ã—â‚š P âˆ£â‚š
    f x = x , x

    f-mono : isMonotonic P (P Ã—â‚š P) f
    f-mono x y xâŠ‘y = xâŠ‘y , xâŠ‘y
```

## Posetal Yoneda lemma

```agda
yoneda : (P : Poset â„“â‚€ â„“â‚)
       â†’ (x y : âˆ£ P âˆ£â‚š)
       â†’ [ (x âŠ‘[ P ] y) â‡” (âˆ€[ z âˆ¶ âˆ£ P âˆ£â‚š ] z âŠ‘[ P ] x â‡’ z âŠ‘[ P ] y) ]
yoneda P x y = forwards , backwards
  where
    open PosetReasoning P

    forwards : [ x âŠ‘[ P ] y â‡’ (âˆ€[ z âˆ¶ âˆ£ P âˆ£â‚š ] z âŠ‘[ P ] x â‡’ z âŠ‘[ P ] y) ]
    forwards xâŠ‘y z zâŠ‘x = z âŠ‘âŸ¨ zâŠ‘x âŸ© x âŠ‘âŸ¨ xâŠ‘y âŸ© y â– 

    backwards : [ (âˆ€[ z âˆ¶ âˆ£ P âˆ£â‚š ] z âŠ‘[ P ] x â‡’ z âŠ‘[ P ] y) â‡’ x âŠ‘[ P ] y ]
    backwards p = p x (âŠ‘[ P ]-refl x)
```

## Galois connections

```agda
module GaloisConnection (P Q : Poset â„“â‚€ â„“â‚) where

  _âŠ£_ : (P â”€mâ†’ Q) â†’ (Q â”€mâ†’ P) â†’ hProp (â„“-max â„“â‚€ â„“â‚)
  f âŠ£ g = âˆ€[ x âˆ¶ âˆ£ P âˆ£â‚š ] âˆ€[ y âˆ¶ âˆ£ Q âˆ£â‚š ] f $â‚˜ x âŠ‘[ Q ] y â‡” x âŠ‘[ P ] g $â‚˜ y

  âŠ£-unique-right : (f : P â”€mâ†’ Q) (gâ‚€ gâ‚ : Q â”€mâ†’ P)
                 â†’ [ f âŠ£ gâ‚€ ] â†’ [ f âŠ£ gâ‚ ] â†’ gâ‚€ â‰¡ gâ‚
  âŠ£-unique-right f gâ‚€ gâ‚ fâŠ£gâ‚€ fâŠ£gâ‚ = forget-mono Q P gâ‚€ gâ‚ (funExt gâ‚€~gâ‚)
    where
      gâ‚€~gâ‚ : (y : âˆ£ Q âˆ£â‚š) â†’ gâ‚€ $â‚˜ y â‰¡ gâ‚ $â‚˜ y
      gâ‚€~gâ‚ y = âŠ‘[ P ]-antisym (gâ‚€ $â‚˜ y) (gâ‚ $â‚˜ y) NTSâ‚€ NTSâ‚
        where
          Î± : [ f $â‚˜ (gâ‚€ $â‚˜ y) âŠ‘[ Q ] y â‡” (gâ‚€ $â‚˜ y âŠ‘[ P ] gâ‚ $â‚˜ y) ]
          Î± = fâŠ£gâ‚ (gâ‚€ $â‚˜ y) y

          Î² : [ (f $â‚˜ (gâ‚€ $â‚˜ y)) âŠ‘[ Q ] y â‡” (gâ‚€ $â‚˜ y) âŠ‘[ P ] (gâ‚€ $â‚˜ y) ]
          Î² = fâŠ£gâ‚€ (gâ‚€ $â‚˜ y) y

          NTSâ‚€ : [ gâ‚€ $â‚˜ y âŠ‘[ P ] gâ‚ $â‚˜ y ]
          NTSâ‚€ = Ï€â‚€ Î± (Ï€â‚ Î² (âŠ‘[ P ]-refl _))

          Ï† : [ (f $â‚˜ (gâ‚ $â‚˜ y) âŠ‘[ Q ] y) â‡” (gâ‚ $â‚˜ y âŠ‘[ P ] gâ‚€ $â‚˜ y) ]
          Ï† = fâŠ£gâ‚€ (gâ‚ $â‚˜ y) y

          Ïˆ : [ f $â‚˜ (gâ‚ $â‚˜ y) âŠ‘[ Q ] y â‡” ((gâ‚ $â‚˜ y) âŠ‘[ P ] (gâ‚ $â‚˜ y))]
          Ïˆ = fâŠ£gâ‚ (gâ‚ $â‚˜ y) y

          NTSâ‚ : [ gâ‚ $â‚˜ y âŠ‘[ P ] gâ‚€ $â‚˜ y ]
          NTSâ‚ = Ï€â‚€ Ï† (Ï€â‚ Ïˆ (âŠ‘[ P ]-refl _))

  GaloisConnection : Type (â„“-max â„“â‚€ â„“â‚)
  GaloisConnection = Î£[ f âˆˆ P â”€mâ†’ Q  ] Î£[ g âˆˆ Q â”€mâ†’ P ] [ f âŠ£ g ]
```


## Poset univalence

Now, we would like to show that ordered structures, as given by `Order`, are a standard
notion of structure. As we have already written down what it means for a function to be
order-preserving, we can express what it means for a *type equivalence* to be order
preserving.

```agda
isAnOrderPreservingEqv : (M N : Î£ (Type â„“â‚€) (Order â„“â‚)) â†’ Ï€â‚€ M â‰ƒ Ï€â‚€ N â†’ Type _ 
isAnOrderPreservingEqv M N e@(f , _) =
  isOrderPreserving M N f Ã— isOrderPreserving N M g
  where
    g = equivFun (invEquiv e)
```

`Order` coupled with `isAnOrdePreservingEqv` gives us an SNS.

```agda
Order-is-SNS : SNS {â„“} (Order â„“â‚) isAnOrderPreservingEqv
Order-is-SNS {â„“ = â„“} {â„“â‚ = â„“â‚} {X = X}  _âŠ‘â‚€_ _âŠ‘â‚_ = f , record { equiv-proof = f-equiv }
  where
    f : isAnOrderPreservingEqv (X , _âŠ‘â‚€_) (X , _âŠ‘â‚_) (idEquiv X) â†’ _âŠ‘â‚€_ â‰¡ _âŠ‘â‚_
    f e@(Ï† , Ïˆ) = funExtâ‚‚ Î» x y â†’ â‡”toPath (Ï† x y) (Ïˆ x y)

    g : _âŠ‘â‚€_ â‰¡ _âŠ‘â‚_ â†’ isAnOrderPreservingEqv (X , _âŠ‘â‚€_) (X , _âŠ‘â‚_) (idEquiv X)
    g p =
      subst
        (Î» _âŠ‘_ â†’ isAnOrderPreservingEqv (X , _âŠ‘â‚€_) (X , _âŠ‘_) (idEquiv X))
        p
        ((Î» _ _ xâŠ‘â‚y â†’ xâŠ‘â‚y) , Î» _ _ xâŠ‘â‚y â†’ xâŠ‘â‚y)

    ret-f-g : retract f g
    ret-f-g (Ï† , Ïˆ) =
      isPropÎ£
        (isOrderPreserving-prop (X , _âŠ‘â‚€_) (X , _âŠ‘â‚_) (id X))
        (Î» _ â†’ isOrderPreserving-prop (X , _âŠ‘â‚_) (X , _âŠ‘â‚€_) (id X))
        (g (f (Ï† , Ïˆ))) (Ï† , Ïˆ)

    f-equiv : (p : _âŠ‘â‚€_ â‰¡ _âŠ‘â‚_) â†’ isContr (fiber f p)
    f-equiv p = ((to , from) , eq) , NTS
      where
        to : isOrderPreserving (X , _âŠ‘â‚€_) (X , _âŠ‘â‚_) (id _)
        to x y = subst (Î» _âŠ‘_ â†’ [ x âŠ‘â‚€ y ] â†’ [ x âŠ‘ y ]) p (id _)

        from : isOrderPreserving (X , _âŠ‘â‚_) (X , _âŠ‘â‚€_) (id _)
        from x y = subst (Î» _âŠ‘_ â†’ [ x âŠ‘ y ] â†’ [ x âŠ‘â‚€ y ]) p (id _)

        eq : f (to , from) â‰¡ p
        eq = Order-set â„“â‚ X _âŠ‘â‚€_ _âŠ‘â‚_ (f (to , from)) p

        NTS : (fib : fiber f p) â†’ ((to , from) , eq) â‰¡ fib
        NTS ((Ï† , Ïˆ) , eq) =
          Î£â‰¡Prop
            (Î» iâ€² â†’ isOfHLevelSuc 2 (Order-set â„“â‚ X) _âŠ‘â‚€_ _âŠ‘â‚_ (f iâ€²) p)
            (Î£â‰¡Prop
               (Î» _ â†’ isOrderPreserving-prop (X , _âŠ‘â‚_) (X , _âŠ‘â‚€_) (id _))
               (isOrderPreserving-prop (X , _âŠ‘â‚€_) (X , _âŠ‘â‚_) (id _) to Ï†))
```

This is the substantial part of the work required to establish univalence for posets.
Adding partial order axioms on top of this is not too hard.

First, let us define what is means for a type equivalence to be monotonic.

```agda
isAMonotonicEqv : (P Q : Poset â„“â‚€ â„“â‚) â†’ âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š â†’ Type _
isAMonotonicEqv (A , (_âŠ‘â‚€_ , _)) (B , (_âŠ‘â‚_ , _)) =
  isAnOrderPreservingEqv (A , _âŠ‘â‚€_) (B , _âŠ‘â‚_)

isAMonotonicEqv-prop : (P Q : Poset â„“â‚€ â„“â‚) (eqv : âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š)
                     â†’ isProp (isAMonotonicEqv P Q eqv)
isAMonotonicEqv-prop P Q e@(f , _) =
  isPropÎ£ (isMonotonic-prop P Q f) Î» _ â†’ isMonotonic-prop Q P g
  where
    g = equivFun (invEquiv e)
```

We denote by `_â‰ƒâ‚š_` the type of monotonic poset equivalences.

```agda
_â‰ƒâ‚š_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€ â„“â‚ â†’ Type _
_â‰ƒâ‚š_ P Q = Î£[ i âˆˆ âˆ£ P âˆ£â‚š â‰ƒ âˆ£ Q âˆ£â‚š ] isAMonotonicEqv P Q i
```

From this, we can already establish that posets form an SNS and prove that the category
of posets is univalent.

```agda
poset-is-SNS : SNS {â„“} (PosetStr â„“â‚) isAMonotonicEqv
poset-is-SNS {â„“â‚ = â„“â‚} =
  UnivalentStrâ†’SNS (PosetStr â„“â‚) isAMonotonicEqv poset-forms-univalent-str
  where
    NTS : (A : Type â„“) (_âŠ‘_ : Order â„“â‚ A) â†’ isProp [ PosetAx A _âŠ‘_ ]
    NTS A _âŠ‘_ = isProp[] (PosetAx A _âŠ‘_)

    poset-forms-univalent-str : UnivalentStr (PosetStr â„“â‚) isAMonotonicEqv
    poset-forms-univalent-str =
      axiomsUnivalentStr _ NTS (SNSâ†’UnivalentStr isAnOrderPreservingEqv Order-is-SNS)

poset-univâ‚€ : (P Q : Poset â„“â‚€ â„“â‚) â†’ (P â‰ƒâ‚š Q) â‰ƒ (P â‰¡ Q)
poset-univâ‚€ = SIP (SNSâ†’UnivalentStr isAMonotonicEqv poset-is-SNS)
```

This result is almost what we want but it is better talk directly about poset
_isomorphisms_ rather than equivalences. In the case when types `A` and `B` are sets, the
type of isomorphisms between `A` and `B` is equivalent to the type of equivalences betwee
them.

Let us start by writing down what a poset isomorphisms is.

```agda
isPosetIso : (P Q : Poset â„“â‚€ â„“â‚) â†’ (P â”€mâ†’ Q) â†’ Type _
isPosetIso P Q (f , _) = Î£[ (g , _) âˆˆ (Q â”€mâ†’ P) ] section f g Ã— retract f g

isPosetIso-prop : (P Q : Poset â„“â‚€ â„“â‚) (f : P â”€mâ†’ Q)
                â†’ isProp (isPosetIso P Q f)
isPosetIso-prop P Q (f , f-mono) (gâ‚€ , secâ‚€ , retâ‚€) (gâ‚ , secâ‚ , retâ‚) =
  Î£â‰¡Prop NTS gâ‚€=gâ‚
  where
    NTS : ((g , _) : Q â”€mâ†’ P) â†’ isProp (section f g Ã— retract f g)
    NTS (g , g-mono) = isPropÎ£
                         (isPropÎ  Î» x â†’ carrier-is-set Q (f (g x)) x) Î» _ â†’
                          isPropÎ  Î» x â†’ carrier-is-set P (g (f x)) x

    gâ‚€=gâ‚ : gâ‚€ â‰¡ gâ‚
    gâ‚€=gâ‚ =
      forget-mono Q P gâ‚€ gâ‚ (funExt Î» x â†’
        Ï€â‚€ gâ‚€ x             â‰¡âŸ¨ sym (cong (Î» - â†’ Ï€â‚€ gâ‚€ -) (secâ‚ x)) âŸ©
        Ï€â‚€ gâ‚€ (f (Ï€â‚€ gâ‚ x)) â‰¡âŸ¨ retâ‚€ (Ï€â‚€ gâ‚ x) âŸ©
        Ï€â‚€ gâ‚ x             âˆ)
```

We will denote by `P â‰…â‚š Q` the type of isomorphisms between posets `P` and `Q`.

```agda
_â‰…â‚š_ : Poset â„“â‚€ â„“â‚ â†’ Poset â„“â‚€ â„“â‚ â†’ Type _
P â‰…â‚š Q = Î£[ f âˆˆ P â”€mâ†’ Q ] isPosetIso P Q f
```

As we have mentioned before, `P â‰…â‚š Q` is equivalent to `P â‰ƒâ‚š Q`.

```agda
â‰ƒâ‚šâ‰ƒâ‰…â‚š : (P Q : Poset â„“â‚€ â„“â‚) â†’ (P â‰…â‚š Q) â‰ƒ (P â‰ƒâ‚š Q)
â‰ƒâ‚šâ‰ƒâ‰…â‚š P Q = isoToEquiv (iso from to ret sec)
  where
    to : P â‰ƒâ‚š Q â†’ P â‰…â‚š Q
    to (e@(f , _) , (f-mono , g-mono)) = (f , f-mono) , (g , g-mono) , sec-f-g , ret-f-g
      where
        is = equivToIso e
        g  = equivFun (invEquiv e)

        sec-f-g : section f g
        sec-f-g = Iso.rightInv (equivToIso e)

        ret-f-g : retract f g
        ret-f-g = Iso.leftInv (equivToIso e)

    from : P â‰…â‚š Q â†’ P â‰ƒâ‚š Q
    from ((f , f-mono) , ((g , g-mono) , sec , ret)) = isoToEquiv is , f-mono , g-mono
      where
        is : Iso âˆ£ P âˆ£â‚š âˆ£ Q âˆ£â‚š
        is = iso f g sec ret

    sec : section to from
    sec (f , _) = Î£â‰¡Prop (isPosetIso-prop P Q) refl

    ret : retract to from
    ret (e , _) = Î£â‰¡Prop (isAMonotonicEqv-prop P Q) (Î£â‰¡Prop isPropIsEquiv refl)
```

Once this equivalence has been established, the main result follows easily: *the category
of posets is univalent*.

```agda
poset-univ : (P Q : Poset â„“â‚€ â„“â‚) â†’ (P â‰…â‚š Q) â‰ƒ (P â‰¡ Q)
poset-univ P Q = P â‰…â‚š Q â‰ƒâŸ¨ â‰ƒâ‚šâ‰ƒâ‰…â‚š P Q âŸ© P â‰ƒâ‚š Q â‰ƒâŸ¨ poset-univâ‚€ P Q âŸ© P â‰¡ Q ğ””ğ”ˆğ”‡
```
